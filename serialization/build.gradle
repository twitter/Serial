/*
 * Copyright 2017 Twitter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Common configuration for all Java libraries: basic properties, checkstyle and JVM tests.
 */

apply plugin: 'java'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'

group = "com.twitter.serial"
version = project.VERSION_NAME
gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.getAllTasks().find{it.path == ":$project.name:generatePomFileForMavenPublication"}.doLast {
        file("build/publications/maven/pom-default.xml").delete()
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                name "Serial"
                artifactId "serial"
                packaging project.name == 'compiler' ? 'jar' : 'aar'
                description 'Light-weight, fast framework for object serialization in Java, with Android support.'
                url 'https://github.com/twitter/serial'
                version project.VERSION_NAME

                scm {
                    url 'git@github.com:twitter/Serial.git'
                    connection 'scm:git:git@github.com:twitter/Serial.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'twitter'
                        name 'Twitter'
                        url 'https://www.twitter.com/'
                    }
                }
            }
        }.writeTo("build/publications/maven/pom-default.xml")
    }
}

publish {
    repoName = 'Serial'
    groupId = 'com.twitter.serial'
    artifactId = 'serial'
    publishVersion = project.VERSION_NAME
    desc = 'Light-weight, fast framework for object serialization in Java, with Android support.'
    website = 'https://github.com/twitter/serial'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
	jcenter()
}

dependencies {
    compile 'org.jetbrains:annotations:13.0'
    testCompile 'org.assertj:assertj-core:2.6.0'
    testCompile 'junit:junit:4.12'
}



