apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'

if (project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}) {
    group = "com.twitter.serial"
    version = project.VERSION_NAME

    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find{it.path == ":$project.name:generatePomFileForMavenPublication"}.doLast {
            file("build/publications/maven/pom-default.xml").delete()
            pom {
                project {
                    name "Serial"
                    artifactId "serial"
                    packaging project.name == 'compiler' ? 'jar' : 'aar'
                    description 'Light-weight, fast framework for object serialization in Java, with Android support.'
                    url 'https://github.com/twitter/serial'
                    version project.VERSION_NAME

                    scm {
                        url 'git@github.com:twitter/Serial.git'
                        connection 'scm:git:git@github.com:twitter/Serial.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'twitter'
                            name 'Twitter'
                            url 'https://www.twitter.com/'
                        }
                    }
                }
            }.writeTo("build/publications/maven/pom-default.xml")
        }
    }
}

publish {
    repoName = 'Serial'
    groupId = 'com.twitter.serial'
    artifactId = 'serial'
    publishVersion = project.VERSION_NAME
    desc = 'Light-weight, fast framework for object serialization in Java, with Android support.'
    website = 'https://github.com/twitter/serial'
}
